plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'signing'
}

group = 'io.github.lambdatest'
version = '1.0.19'
description = 'lambdatest-java-sdk'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.json:json:20231013'
//    compileOnly 'org.seleniumhq.selenium:selenium-java:[4.0.0,)'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.netty:netty-transport-native-epoll:4.1.104.Final'
    implementation 'io.netty:netty-transport-native-kqueue:4.1.104.Final'
    implementation 'org.seleniumhq.selenium:selenium-java:4.1.2'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
}

shadowJar {
    archiveClassifier.set('')
    archiveVersion.set(version)
    manifest {
        attributes(
            'Main-Class': 'io.github.lambdatest.MainClass'
        )
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

// Ensure you have signing credentials for Maven Central
signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // Additional configuration for your publication if needed
        }
    }

    repositories {
        maven {
            name = "sonatype"
            url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
        maven {
            name = "sonatypeSnapshots"
            url = uri("https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/")
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'io.github.lambdatest'
                artifactId = 'lambdatest-java-sdk'
                version = '1.0.19'

                pom {
                    name.set('LambdaTest Java SDK')
                    description.set('A Java SDK for LambdaTest services.')
                    url.set('https://github.com/lambdatest/lambdatest-java-sdk')

                    licenses {
                        license {
                            name.set('The Apache License, Version 2.0')
                            url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
                        }
                    }

                    developers {
                        developer {
                            id.set('shahnawazsk')
                            name.set('Shahnawaz Sk')
                            email.set('shahnawaz@lambdatest.com')
                        }
                    }

                    scm {
                        connection.set('scm:git:git://github.com/lambdatest/lambdatest-java-sdk.git')
                        developerConnection.set('scm:git:ssh://github.com:lambdatest/lambdatest-java-sdk.git')
                        url.set('https://github.com/lambdatest/lambdatest-java-sdk')
                    }
                }
            }
        }
    }
}